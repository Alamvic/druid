"
Implements a basic interpreter to compiler.

- All the generated bytecodes or primitives are unimplemented.
- No error handling.

This enables to build a VM which includes the JIT, but does not know how to JIT any method. Then the primitives or bytecodes could be added incrementally, and provides a baseline to compare performance between implementations.



"
Class {
	#name : #DRInterpreterToCompiler,
	#superclass : #DRAbstractCompilerBuilder,
	#instVars : [
		'memory',
		'compilerCompilerClass',
		'compilerCompiler'
	],
	#category : #'Druid-CompilerBuilder'
}

{ #category : #examples }
DRInterpreterToCompiler class >> examplePrimitivesFromCollection [
	<example>
	
	^ (DRInterpreterToCompiler 
			fromInterpreterBuilder: (DRInterpreterBuilder new primitives: #(#primitiveAdd)))
				primitives inspect
]

{ #category : #examples }
DRInterpreterToCompiler class >> examplePrimitivesFromInterpreterBuilderClass [
	<example>
	
	^ (DRInterpreterToCompiler 
			fromInterpreterBuilder: DRInterpreterBuilder new)
				primitives inspect
]

{ #category : #'instance creation' }
DRInterpreterToCompiler class >> fromInterpreter: anInterpreterBuilder compilerClass: aCompilerClass [

	^ self basicNew
		compilerCompilerClass: aCompilerClass;
		interpreterBuilder: anInterpreterBuilder;
		initialize;
		yourself
]

{ #category : #'instance creation' }
DRInterpreterToCompiler class >> fromInterpreterBuilder: anInterpreterBuilder [

	^  self basicNew
		interpreterBuilder: anInterpreterBuilder;
		initialize;
		yourself
]

{ #category : #'instance creation' }
DRInterpreterToCompiler class >> fromInterpreterClass: anInterpreterClass [

	^  self basicNew
		interpreterClass: anInterpreterClass;
		yourself
]

{ #category : #'accessing - compiler' }
DRInterpreterToCompiler >> compilationUnitClass [

	^ DRPrimitiveCompilationUnit 
]

{ #category : #'accessing - compiler' }
DRInterpreterToCompiler >> compilerCompiler [
	"Answer a Druid compiler configured for the receiver's interpreter builder"

	^ compilerCompiler
		ifNil: [ compilerCompiler := 
			(self compilerCompilerClass forInterpreter: self newInterpreter)
					compilerClass: self targetClass;
					yourself ]
]

{ #category : #'accessing - compiler' }
DRInterpreterToCompiler >> compilerCompilerClass [

	^ compilerCompilerClass
		ifNil: [ compilerCompilerClass := self defaultCompilerCompilerClass ]
]

{ #category : #'accessing - compiler' }
DRInterpreterToCompiler >> compilerCompilerClass: aClass [

	compilerCompilerClass := aClass
]

{ #category : #'accessing - compiler' }
DRInterpreterToCompiler >> defaultCompilerCompilerClass [

	^ DRPrimitiveCompilerCompiler
]

{ #category : #'accessing - model' }
DRInterpreterToCompiler >> generateBuildModel [
	"Answer a <DRCompilationUnit>, useful for model manipulation before code dumping"

	self addPrimitives.
	^ self compilationUnit
]

{ #category : #'accessing - model' }
DRInterpreterToCompiler >> generateBuildModelAndCompileIn: aClass [
	"Generate and install the receiver's primitives into JIT compiler aClass"

	self generateBuildModel
		interpreter: interpreterClass;
		targetClass: (self environmentAt: aClass);
		compileAll
]

{ #category : #'accessing - model' }
DRInterpreterToCompiler >> generateBuildModelAndCompileIn: aClass superclass: aSuperclass [
	"Generate and install the receiver's primitives into JIT compiler aClass"

	self generateBuildModel
		interpreter: interpreterClass;
		targetSuperclass: aSuperclass;
		targetClass: (self environmentAt: aClass);
		compileAll
]

{ #category : #'accessing - model' }
DRInterpreterToCompiler >> generateIRAndCompileIn: aClass [
	"Generate and install the receiver's primitives into JIT compiler aClass"

	self compilationUnit
		targetClass: (self environmentAt: aClass);
		compiler: self compilerCompiler;
		addPrimitives;
		compileAll
]

{ #category : #helpers }
DRInterpreterToCompiler >> initPrimitiveTable [
	"Answer a <Collection> with the JITed primitives"

	^ compilationUnit initPrimitiveTable
]

{ #category : #'accessing - object memory' }
DRInterpreterToCompiler >> initializationOptions [

	^ {
		#BytesPerWord . 8 .
		#ObjectMemory .  memory class name
		}
]

{ #category : #accessing }
DRInterpreterToCompiler >> interpreterClass [

	^ interpreterClass
]

{ #category : #accessing }
DRInterpreterToCompiler >> interpreterClass: anObject [

	interpreterClass := anObject
]

{ #category : #accessing }
DRInterpreterToCompiler >> interpreterPrimitiveTable [
	"Answer a <Collection> of <Symbol> each one representing a selector (e.g. #primitiveEqual) of the receiver's primitive table"

	^ interpreterClass primitiveTable
]

{ #category : #'instance creation' }
DRInterpreterToCompiler >> newInterpreter [
	" Answer an <AbstractInterpreter|StackInterpreter> from the receiver's interpreter builder "

	memory := self newMemory.

	^ interpreterClass basicNew
		  objectMemory: memory;
		  basicInitialize
]

{ #category : #'accessing - object memory' }
DRInterpreterToCompiler >> newMemory [
	" Answer a memory manager instance. Currently a subclass of <SpurMemoryManager> "

	^ Spur64BitCoMemoryManager simulatorClass new
]

{ #category : #'accessing - primitives' }
DRInterpreterToCompiler >> primitiveTable [
	"Answer a <Collection> of primitive selectors"

	^ interpreterClass basicPrimitiveTable
]

{ #category : #'accessing - primitives' }
DRInterpreterToCompiler >> primitives [

	^ self compilationUnit primitives
		ifEmpty: [ self generateBuildModel primitives ]
		ifNotEmpty: [ : p | p ]
]
